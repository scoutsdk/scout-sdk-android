apply plugin: 'com.android.library'
apply plugin: 'com.apollographql.android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def publishVersion = '0.1.1'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName publishVersion
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/sdk-release.aar")
            groupId 'com.scoutsdk.sdk'
            artifactId 'sdk'
            version publishVersion

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

bintray {
    File localProperties = project.rootProject.file('local.properties')
    Properties properties = new Properties()
    if (localProperties.exists()) {
        properties.load(localProperties.newDataInputStream())
    }

    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        userOrg = 'scoutsdk'
        repo = 'maven'
        name = 'com.scoutsdk.sdk'
        desc = ''
        websiteUrl = 'https://www.scoutsdk.com'
        vcsUrl = 'https://github.com/scoutsdk/scout-sdk-android.git'
        licenses = ['MIT']
        publish = true
        publicDownloadNumbers = true
        version {
            name = publishVersion
            vcsTag = publishVersion
            released = new Date()
        }
    }
    publications = ['Production']
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:customtabs:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:support-media-compat:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.google.android.gms:play-services-ads:15.0.1'
    implementation 'com.google.ads.mediation:facebook:4.28.2.1'
    implementation 'com.facebook.android:audience-network-sdk:4.99.1'
    implementation 'com.squareup.moshi:moshi-kotlin:1.6.0'
    api 'net.sourceforge.streamsupport:android-retrostreams:1.6.3'
    api 'com.apollographql.apollo:apollo-runtime:1.0.0-alpha3'
    api 'com.apollographql.apollo:apollo-android-support:1.0.0-alpha3'
}
