apply plugin: 'com.android.library'
apply plugin: 'com.apollographql.android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'digital.wup.android-maven-publish'

project.version = '0.1.3'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName project.version
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/java'
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.sourceFiles
    classifier 'sources'
}

publishing {
    publications {
        Production(MavenPublication) {
            from components.android
            artifact(sourceJar)
            artifact(bundleReleaseAar)
            groupId 'com.scoutsdk.sdk'
            artifactId 'sdk'
            version project.version
        }
    }
}

bintray {
    File localProperties = project.rootProject.file('local.properties')
    Properties properties = new Properties()
    if (localProperties.exists()) {
        properties.load(localProperties.newDataInputStream())
    }

    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'com.scoutsdk.sdk'
        name = 'com.scoutsdk.sdk'
        desc = 'Build powerful gaming community apps with magic. Scout delivers gaming stats to apps and websites.'
        websiteUrl = 'https://www.scoutsdk.com'
        vcsUrl = 'https://github.com/scoutsdk/scout-sdk-android.git'
        licenses = ['MIT']
        publish = true
        publicDownloadNumbers = true
        version {
            name = project.version
            vcsTag = project.version
            released = new Date()
        }
    }
    publications = ['Production']
}

task releaseForBintrayUpload(dependsOn: [
        'clean',
        'assembleRelease',
        'generatePomFileForProductionPublication',
        'sourceJar'
])
bintrayUpload.dependsOn(releaseForBintrayUpload)

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:customtabs:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:support-media-compat:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.google.android.gms:play-services-ads:15.0.1'
    implementation 'com.google.ads.mediation:facebook:4.28.2.1'
    implementation 'com.facebook.android:audience-network-sdk:4.99.1'
    implementation 'com.squareup.moshi:moshi-kotlin:1.6.0'
    api 'net.sourceforge.streamsupport:android-retrostreams:1.6.3'
    api 'com.apollographql.apollo:apollo-runtime:1.0.0-alpha3'
    api 'com.apollographql.apollo:apollo-android-support:1.0.0-alpha3'
}
